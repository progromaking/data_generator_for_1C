
Функция СоздатьДанныеПоТабличномуДокументу(Макет) Экспорт
	
	ПеременныеТекущегоСценария = Новый Структура;
	
	Для счСтр = 1 по Макет.ВысотаТаблицы Цикл
		// Читаем первую колонку макета - шапка данных
		ИмяПараметра = Макет.Область(счСтр, 1).Текст;
		ЗначениеПараметра = Макет.Область(счСтр, 2).Текст;
		Если ПустаяСтрока(ИмяПараметра) Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПеременной = Неопределено;
		ИмяПеременной = Неопределено;
		
		ИмяПараметра = Макет.Область(счСтр, 1).Текст;
		ИмяПараметра = СокрЛП(ИмяПараметра);
		ИмяПараметра = ВРЕГ(ИмяПараметра);
		
		ЗначениеПараметра = Макет.Область(счСтр, 2).Текст;
		ЗначениеПараметра = СокрЛП(ЗначениеПараметра);
		Пока ЗначениеЗаполнено(ИмяПараметра) Цикл
			
			Если ИмяПараметра = "ТИП" Тогда
				ТипПеременной = ЗначениеПараметра;
			ИначеЕсли ИмяПараметра = "ИМЯ" Тогда
				ИмяПеременной = ЗначениеПараметра;
			КонецЕсли;
			
			счСтр = счСтр + 1;
			ИмяПараметра = врег(Макет.Область(счСтр, 1).Текст);
			ЗначениеПараметра = Макет.Область(счСтр, 2).Текст;
		КонецЦикла;
		
		Если ТипПеременной = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Читаем третью колонку макета - таблица данных
		НачСтрока = счСтр; // НачСтрока - Имена колонок таблицы, НачСтрока+1 - Описание типов (может быть незаполнено)
		счСтр = счСтр + 2;
		Пока ЗначениеЗаполнено(Макет.Область(счСтр, 3).Текст) Цикл
			КонСтрока = счСтр;
			счСтр = счСтр + 1;
		КонецЦикла;
		
		Область = Макет.ПолучитьОбласть(НачСтрока,,КонСтрока);
		Попытка
			ТаблицаИзМакета = ПрочитатьТаблицуЗначенийИзМакета(Область);
		Исключение
			Описание = ОписаниеОшибки();
			Описание = "Ошибка при чтении макета в строках с "+НачСтрока + " по " + КонСтрока +" : " + Описание;
			ВызватьИсключение Описание;
		КонецПопытки;
		
		ПостфиксМетода = Лев(ТипПеременной, Найти(ТипПеременной, ".")-1);
		Если ПустаяСтрока(ПостфиксМетода) Тогда
			ПостфиксМетода = ТипПеременной;
		КонецЕсли;
		ВидМетаданных = СтрЗаменить(ТипПеременной, ПостфиксМетода+".", "");
		
		Попытка
			Результат = Вычислить("ЗаполнитьИзТаблицыЗначений_" + ПостфиксМетода + "(ТаблицаИзМакета,ВидМетаданных)");
		Исключение
			Описание = ОписаниеОшибки();
			Описание = "Ошибка при создании данных из макета в строках с "+НачСтрока + " по " + КонСтрока +" : " + Описание;
			ВызватьИсключение Описание;
		КонецПопытки;
		
		Если ЗначениеЗаполнено(ИмяПеременной) Тогда
			ПеременныеТекущегоСценария.Вставить(ИмяПеременной, Результат);
		КонецЕсли;
		
		СчСтр = счСтр - 1;
		
	КонецЦикла;
	
	Возврат ПеременныеТекущегоСценария;
КонецФункции

Функция ПрочитатьТаблицуЗначенийИзМакета(Область)
	
	ДополнительныеПараметрыКолонок = Новый Структура;
	
	ТаблицаИзМакета = Новый ТаблицаЗначений;
	Для счКол = 3 по Область.ШиринаСтраницы Цикл
		ИмяКолонки = Область.Область(1, счКол).Текст;
		Если ПустаяСтрока(ИмяКолонки) Тогда
			Прервать;
		КонецЕсли;
		
		ТипКолонки = Область.Область(2, счКол).Текст;
		Поз = Найти(ТипКолонки, "/");
		Если Поз Тогда
			ДополнительныйПараметр = Сред(ТипКолонки, Поз+1);
			ДополнительныеПараметрыКолонок.Вставить(ИмяКолонки, ДополнительныйПараметр);
			ТипКолонки = СтрЗаменить(ТипКолонки, "/"+ДополнительныйПараметр, "");
		Иначе
			ДополнительныеПараметрыКолонок.Вставить(ИмяКолонки, "");
		КонецЕсли;
		Если ПустаяСтрока(ТипКолонки) Тогда
			ТаблицаИзМакета.Колонки.Добавить(ИмяКолонки);
		Иначе
			ТаблицаИзМакета.Колонки.Добавить(ИмяКолонки, Новый ОписаниеТипов(ТипКолонки));
		КонецЕсли;
	КонецЦикла;
	
	Для счСтр = 3 по Область.ВысотаТаблицы Цикл
		Если ПустаяСтрока(Область.Область(счСтр, 3).Текст) Тогда
			Прервать;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаИзМакета.Добавить();
		Для счКол = 1 по ТаблицаИзМакета.Колонки.Количество() Цикл
			Колонка = ТаблицаИзМакета.Колонки[счКол-1];
			Значение = Область.Область(счСтр, счКол+2).Текст;
			ТипыКолонки = Колонка.ТипЗначения.Типы();
			
			Если ТипыКолонки.Количество() Тогда
				мдТип = Метаданные.НайтиПоТипу(ТипыКолонки[0]);
			Иначе
				мдТип = Неопределено;
			КонецЕсли;
			
			Если Найти(Значение, "=") = 1 Тогда
				Значение = Вычислить(Сред(Значение, 2));
			ИначеЕсли мдТип = Неопределено Тогда
				Значение = Строка(Значение);
			ИначеЕсли Метаданные.Перечисления.Содержит(мдТип) Тогда
				Значение = НайтиПеречисление(Значение, мдТип);
				
			ИначеЕсли Метаданные.Справочники.Содержит(мдТип) Тогда
				ДополнительныйПараметр = ДополнительныеПараметрыКолонок[Колонка.Имя];
				Значение = НайтиСоздатьОбъектСКодом(Значение, мдТип, ДополнительныйПараметр);
			ИначеЕсли Метаданные.ПланыВидовХарактеристик.Содержит(мдТип) Тогда
				ДополнительныйПараметр = ДополнительныеПараметрыКолонок[Колонка.Имя];
				Значение = НайтиСоздатьОбъектСКодом(Значение, мдТип, ДополнительныйПараметр);
				
			ИначеЕсли Метаданные.Документы.Содержит(мдТип) Тогда
				Значение = НайтиСоздатьОбъектСНомером(Значение, мдТип);
			ИначеЕсли Метаданные.Задачи.Содержит(мдТип) Тогда
				Значение = НайтиСоздатьОбъектСНомером(Значение, мдТип);
			ИначеЕсли Метаданные.БизнесПроцессы.Содержит(мдТип) Тогда
				Значение = НайтиСоздатьОбъектСНомером(Значение, мдТип);
			КонецЕсли;
			
			НоваяСтрока[Колонка.Имя] = Значение;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаИзМакета;
КонецФункции

Функция НайтиСоздатьОбъектСНомером(Номер, ТипМетаданных)
	Дата = Дата("20000101");
	Если Метаданные.Документы.Содержит(ТипМетаданных) Тогда
		Менеджер = Документы[ТипМетаданных.Имя];
		МетодСоздания = "СоздатьДокумент";
	ИначеЕсли Метаданные.Задачи.Содержит(ТипМетаданных) Тогда
		Менеджер = Задачи[ТипМетаданных.Имя];
		МетодСоздания = "СоздатьЗадачу";
	ИначеЕсли Метаданные.БизнесПроцессы.Содержит(ТипМетаданных) Тогда
		Менеджер = БизнесПроцессы[ТипМетаданных.Имя];
		МетодСоздания = "СоздатьБизнесПроцесс";
	КонецЕсли;
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат Менеджер.ПустаяСсылка();
	КонецЕсли;
	
	ОбъектСсылка = НайтиПоНомеру(Номер, ТипМетаданных.ПолноеИмя()); // для документов с периодичной нумерацией стандартный метод НайтиПоНомеру() не работает без указания даты, поэтому ищем запросом
	Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Возврат ОбъектСсылка;
	КонецЕсли;
	
	ОбъектОбъект = Вычислить("Менеджер."+МетодСоздания + "()");
	ОбъектОбъект.Номер = Номер;
	ОбъектОбъект.Дата = Дата;
	ОбъектОбъект.ОбменДанными.Загрузка = Истина;
	ОбъектОбъект.Записать();
	Возврат ОбъектОбъект.Ссылка;
	
КонецФункции
Функция НайтиСоздатьОбъектСКодом(Код, ТипМетаданных, ДополнительныйПараметр)
	Если Метаданные.Справочники.Содержит(ТипМетаданных) Тогда
		Менеджер = Справочники[ТипМетаданных.Имя];
	ИначеЕсли Метаданные.ПланыВидовХарактеристик.Содержит(ТипМетаданных) Тогда
		Менеджер = ПланыВидовХарактеристик[ТипМетаданных.Имя];
	КонецЕсли;
	
	Если ПустаяСтрока(Код) Тогда
		Возврат Менеджер.ПустаяСсылка();
	КонецЕсли;
	
	ЭтоОбъектБезКода = ТипМетаданных.ДлинаКода = 0;
	Если ЭтоОбъектБезКода Тогда
		Ссылка = Менеджер.НайтиПоНаименованию(Код);
	Иначе
		Ссылка = Менеджер.НайтиПоКоду(Код);
	КонецЕсли;
	
	Если НЕ Ссылка.Пустая() Тогда
		Возврат Ссылка;
	КонецЕсли;
	
	Если НРег(ДополнительныйПараметр) = "группа" Тогда
		Объект = Менеджер.СоздатьГруппу();
	Иначе
		Объект = Менеджер.СоздатьЭлемент();
	КонецЕсли;
	
	Буфер = Новый Структура("Наименование,Код", Код, Код);
	ЗаполнитьЗначенияСвойств(Объект, Буфер);
	
	Объект.ОбменДанными.Загрузка = Истина;
	Объект.Записать();
	
	Возврат Объект.Ссылка;
	
КонецФункции
Функция НайтиПеречисление(Идентификатор, ТипМетаданных)
	Если ПустаяСтрока(Идентификатор) Тогда
		Возврат Перечисления[ТипМетаданных.Имя].ПустаяСсылка();
	КонецЕсли;
	Возврат  Перечисления[ТипМетаданных.Имя][Идентификатор];
КонецФункции
Функция НайтиПоНомеру(Номер, ИмяТаблицы)
	Перем Запрос;
	Запрос = Новый Запрос;
	Запрос.Текст = "выбрать первые 1 т.Ссылка из ИмяТаблицы как т где т.Номер=&Номер";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицы", ИмяТаблицы);
	Запрос.УстановитьПараметр("Номер", Номер);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

Функция ЗаполнитьИзТаблицыЗначений_ТаблицаЗначений(ТаблицаИзМакета, Вид)
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Структура(ТаблицаИзМакета, Вид)
	СтруктураИзМакета = Новый Структура;
	Для каждого Колонка из ТаблицаИзМакета.Колонки Цикл
		СтруктураИзМакета.Вставить(Колонка.Имя);
	КонецЦикла;
	ЗаполнитьЗначенияСвойств(СтруктураИзМакета, ТаблицаИзМакета[0]);
	Возврат СтруктураИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Документ(ТаблицаИзМакета, Вид)
	Возврат ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, Вид);
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Справочник(ТаблицаИзМакета, Вид)
	Возврат ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, Вид);
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_ПланВидовХарактеристик(ТаблицаИзМакета, Вид)
	Возврат ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, Вид);
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Задача(ТаблицаИзМакета, Вид)
	Возврат ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, Вид);
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_БизнесПроцесс(ТаблицаИзМакета, Вид)
	Возврат ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, Вид);
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_РегистрСведений(ТаблицаИзМакета, ИмяРегистра)
	Если НРег(ТаблицаИзМакета.Колонки[0].Имя) = "регистратор" Тогда
		Возврат ЗаполнитьИзТаблицыЗначений_ДвиженияДокумента(ТаблицаИзМакета, ИмяРегистра);
	КонецЕсли;
	
	Для каждого Строка из ТаблицаИзМакета Цикл
		Менеджер = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Менеджер, Строка);
		Менеджер.Прочитать();
		ЗаполнитьЗначенияСвойств(Менеджер, Строка);
		Менеджер.Записать(Истина);
	КонецЦикла;
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_РегистрНакопления(ТаблицаИзМакета, ИмяРегистра)
	ЗаполнитьИзТаблицыЗначений_ДвиженияДокумента(ТаблицаИзМакета, ИмяРегистра);
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Константы(ТаблицаИзМакета, ДопПараметр)
	
	Для каждого Колонка из ТаблицаИзМакета.Колонки Цикл
		ИмяКонстанты = Колонка.Имя;
		Константы[ИмяКонстанты].Установить(ТаблицаИзМакета[0][ИмяКонстанты]);
	КонецЦикла;
	
	Возврат ТаблицаИзМакета;
КонецФункции

Функция ЗаполнитьИзТаблицыЗначений_ДвиженияДокумента(ТаблицаИзМакета, ИмяРегистра)
	
	Для каждого Строка из ТаблицаИзМакета Цикл
		Движения = Строка
					.Регистратор
					.ПолучитьОбъект()
					.Движения[ИмяРегистра];
		Движения.Прочитать();
		НовоеДвижение = Движения.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеДвижение, Строка);
		Если НовоеДвижение.Период = Дата(1,1,1) Тогда
			НовоеДвижение.Период = НовоеДвижение.Регистратор.Дата;
		КонецЕсли;
		Движения.Записать(Истина);
	КонецЦикла;
	
	Возврат ТаблицаИзМакета;
КонецФункции
Функция ЗаполнитьИзТаблицыЗначений_Объект(ТаблицаИзМакета, ВидОбъекта)
	ИмяТабличнойЧасти = "";
	Если Найти(нрег(ВидОбъекта), ".табличнаячасть.") Тогда
		Буфер = СтрЗаменить(нрег(ВидОбъекта), ".табличнаячасть.", Символы.ПС);
		ИмяТабличнойЧасти = СтрПолучитьСтроку(Буфер, 2);
	КонецЕсли;
	
	Для каждого Строка из ТаблицаИзМакета Цикл
		Объект = Строка.Ссылка.ПолучитьОбъект();
		
		
		Если ПустаяСтрока(ИмяТабличнойЧасти) Тогда
			ЗаполнитьЗначенияСвойств(Объект, Строка);
		Иначе
			ЗаполнитьЗначенияСвойств(Объект[ИмяТабличнойЧасти].Добавить(), Строка);
		КонецЕсли;
		Объект.ОбменДанными.Загрузка = Истина;
		Объект.Записать();
	КонецЦикла;
	
	Возврат ТаблицаИзМакета;
КонецФункции
